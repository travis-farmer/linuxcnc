component MacSafety "manage safety interlocks, and display signal tower";

pin out bit SignalTowerRed "Signal Tower RED output";
pin out bit SignalTowerAmber "Signal Tower Amber output";
pin out bit SignalTowerGreen "Signal Tower Green output";
pin out bit SignalTowerWhite "Signal Tower WHite output";
pin out bit SignalTowerBlue "Signal Tower Blue output";
pin out bit SignalTowerBuzzer "Signal Tower Buzzer output";
pin out bit SignalTowerBuzzernot "NOT Signal tower buzzer output";
pin out bit EstopFault "trigger Estop output";
pin out bit EstopOK "trigger Estop OK";
pin out bit EstopReset "trigger estop reset";
pin out bit ATCairSafe "ATC Air in safe range";
pin in bit ProgPausedIn "halui.program.is-paused connection";
pin in bit ProgRunningIn "halui.program.is-running connection";
pin in bit ProgIdleIn "halui.program.is-idle connection";
pin in bit EstopTrigger "Trigger from external";
pin in bit EstopIn "input from ESTOP";
pin in bit NGCwarning "input from NGC warning";
pin in bit ManualChangeWaiting "Waiting for manual tool change";
pin in bit ATCActive "ATC is ACTIVE LED";
pin in float ATCair "ATC Air input";
pin in float ATCairTrigger "ATC less than air trigger";
pin in float SpindTemp "Spindle temperature";
pin in float SpindTempTrigger "Spindle greater than Temp Trigger";
pin in float SpindHP "Spindle HP input";
pin in float SpindHPtrigger "Spindle greater than HP trigger";

function _;
license "GPL";
author "Travis Farmer";
;;


FUNCTION(_) {
	
    // status LED Tower - red, amber, green, blue, white, buzzer
    if (EstopIn == false || EstopTrigger == false) {
        SignalTowerGreen = false;
        SignalTowerAmber = false;
        SignalTowerRed = true;
        SignalTowerBuzzer = true;
        SignalTowerBuzzernot = false;
        if (EstopFault == false) {
            EstopFault = true;
            EstopReset = false;
        }
    } else if (ProgPausedIn == true || (ATCair <= ATCairTrigger || SpindTemp >= SpindTempTrigger || SpindHP >= SpindHPtrigger || NGCwarning == true)) {
        SignalTowerGreen = false;
        SignalTowerAmber = true;
        SignalTowerRed = false;
        SignalTowerBuzzer = false;
        SignalTowerBuzzernot = true;
    } else if (ProgRunningIn == true) {
        SignalTowerGreen = true;
        SignalTowerAmber = false;
        SignalTowerRed = false;
        SignalTowerBuzzer = false;
        SignalTowerBuzzernot = true;
    } else {
        SignalTowerGreen = false;
        SignalTowerAmber = false;
        SignalTowerRed = false;
        SignalTowerBuzzer = false;
        SignalTowerBuzzernot = true;
    }
    if (ATCActive == true) {
        SignalTowerWhite = true;
    } else {
        SignalTowerWhite = false;
    }
    if (ManualChangeWaiting == true) {
        SignalTowerBlue = true;
    } else {
        SignalTowerBlue = false;
    }
    if (EstopIn == true && EstopTrigger == true) {
        EstopFault = false;
        EstopReset = true;
    }
    if (ATCair > ATCairTrigger) {
        ATCairSafe = true;
    } else {
        ATCairSafe = false;
    }
    if (EstopFault == true) {
        EstopOK = false;
    } else {
        EstopOK = true;
    }
}
